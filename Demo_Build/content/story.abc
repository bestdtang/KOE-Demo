@resume
    *say
        Welcome back! Do you want to continue your story from where you last left
        off?
    *then
        hear yes {
            >> RESUME
        }

        hear no {
            -> start
        }

@start
	*say
		Greetings, explorer. Welcome to the Desert Adventurer experience.
	*then
    	set currentLocation as 'desert'
		-> Lost in Desert

@Lost in Desert
	*say
		The blowing, hot wind scorches the surface of both the earth and your skin as you walk aimlessly in the desert, thirsty for water. [Pause] [gulp] [footstep] In the middle of the hot desert, you found a rusty, old signpost. It has two arrows pointing to the right and the left, but their names have faded away.
	*then
		-> Tutorial 1: Direction Questions

@Tutorial 1: Direction Questions
	*say
		[DirectionQuestionSound]When you hear this sound, you will be deciding which path, or direction, to take. In this case, try saying: go to the left, or go to the right.
	*then
		hear go to the left {
			-> Look Left
		}

		hear go to the right {
			-> Look Right
		}
	*reprompt
		You can say, look left or look right.
	*recap
		[Pause]

@Look Right
	*say
		You wiped your face with your dry hands and decided to turn right. Then, you keep treading the sea of powdered rocks wearily. 
	*then
		-> Tutorial 1A: Check Around

@Tutorial 1A: Check Around
	*say
		You walked under the hot sun until you have reached the top of a little sand hill. It grants you some height advantage and now you can see what's around you. [Pause] [DirectionQuestionSound]Sometimes, paths aren't obvious at first glance, like now. You can check around first by saying what's around me. Go on, give it a go.
	*then
		hear what's around me, around me {
            -> Check Around the Hill
        }
	*reprompt
		You can check around by saying "check what's around me".
	*recap
		You can check around by saying "check what's around me".

@Check Around the Hill
	*say
		You looked around, like a compass slowly spinning. You don't find anything particularly interesting- wait, you see a few freckles on the yellow surfaces of Earth. It shines under the burning sun! You squint your eyes and look closer. it's a town! It's a town! [pause] There's a good chance that they have some water there.[DirectionQuestionSound]
	*then
		hear go to the town {
		    set currentLocation as 'town'
             -> Go to Town
         }
	*reprompt
		You might want to go to a location?
	*recap
		You might want to go to a location? try say go to the town?

@Go to Town
	*say
		Quick! There might be water there.
	*then
		-> Town

@Town
	*say
		The urge of cold water passing through dry lungs has dragged your weary feet to the entrance of the town. You thought the townspeople would step forward immediately to help a thirsty traveler, but instead... [Pause] No-one came. What's happening? [DirectionQuestionSound] you can ask to check the environment. Try saying: "what's around me?"
	*recap
		you can ask to check the environment. Try saying: "what's around me?"
	*reprompt
		you can ask to check the environment. Try saying: "what's around me?"
	*then
		hear what's around me {
			-> Around 
		}

@Around
	*say
		You're at the entrance of the town. Some small buildings align on the sides of the street, but all of their doors are shut tight. Only a few people are walking on the streets, but it's almost as if they're trying to avoid having any eye-contact with you.
	*reprompt
		There are a few establishments right in front of you: a general store, the mayor's office. Could any of them have some water? Choose a location to go to.
	*recap
		There are a few establishments right in front of you: a general store, the mayor's office. Could any of them have some water? Choose a location to go to.
	*then
		-> Around_1

@Around_1
	*say
		There are a few establishments right in front of you: a general store, the mayor's office. Could any of them have some water? [DirectionQuestionSound]
	*recap
		There are a few establishments right in front of you: a general store, the mayor's office. Could any of them have some water? Choose a location to go to.
	*reprompt
		There are a few establishments right in front of you: a general store, the mayor's office. Could any of them have some water? Choose a location to go to.
	*then
		hear mayor's office, office, mayor, go to mayor's office {
		    set currentLocation as 'mayors office'
			-> Mayor Office
		}
		hear general store, general, store, go to the general store {
		    set currentLocation as 'general store'
			-> Store
		}

@Store
	*say
		You go to the general store. Upon approaching the general store, you see a boy. He wears a red coat, and you can see "surprised" written all over his face. [Pause][Kid] You are a new face here. Do you need help? You don't look well.[/Kid] [Pause] Maybe he has water. You can probably ask if there's water. [ConversationQuestionSound]When you hear this sound, you decide what to say to the other person next. For example, right now you can say: ask the boy for water. Try it.
	*then
		hear ask for water, ask the boy, ask the boy for water {
			-> Boy
		}

@Boy
	*say
		You asked the boy for some water. 
		[Kid] 
	    I can help you with that. But I must warn you- that situations in the house aren't exactly desirable. In a sense.[/Kid] 
		The boy takes you around the building and enters the store from the backdoor. [Pause] Oh dear it was far from being desirable. The air in the store was nothing of this world. It's foul and revolting. Each step you take is like a deeper dive into the bowels of a carnivorous beast. [Pause]
		[Kid] 
		Yeah. Told you. Papa![/Kid] 
		A bald, middle-aged man that looks like in his fifties are slowly walks down the stairs. Small pieces of tissue paper are rolled and stuck in his nose. He looks like he could use some more sunshine.
	*then
		-> Shopkeeper

@Shopkeeper
	*say
		[Man]
        Who is this person? Why did you bring a stranger that looks like a dying cactus to this place?[/Accent End] 
        [Kid] 
        Relax, papa. He's just a traveler. And he needs water. A lot of water.[/Kid] 
        [Man] 
        Water? Hah! I got lots of water. As if water would do anything to wash this stench away.[/Accent End] 
        [Pause] 
        The young boy's father handed you a big glass of water. 
        [Man] 
        Hey- dont worry. I know it smells bad in here. But I assure you, the water is clean. Go on, drink up. Ahh. Yes, the water is fresh and good.[/Accent End]
        [Pause]
        [Action]
        Sometimes when appropriate, you can use an item, perform an action, or do nothing. For this instance, you can either say: "use water", or "do nothing".
	*then
		hear drink water, use water, use the water {
			-> Finding Out
		}
		hear don't drink the water, don't, no, do nothing {
			-> Don't Drink
		}
	*recap
		Sometimes when appropriate, you can use an item, perform an action, or do nothing. For this instance, you can either say: "use water", or "do nothing". 
	*reprompt
		Sometimes when appropriate, you can use an item, perform an action, or do nothing. For this instance, you can either say: "use water", or "do nothing".

@Don't Drink
	*say
		You hold the glass of water at your hand, but you don't drink from it. 
    [Man]
    Hmmmm. [/Accent End]
    The shopkeeper looks at the glass of water. He looks down on the dusty floor.
    [Man]
    Suit yourself. Thought you were really thirsty. [/Accent End]
    At least you're in the shade, so you're starting to feel a little bit less thirsty. But still, the stench is unbearable.
    [Man]
    Maybe the smell even went in the water, but we just don't know. Heh. It wasn't like this before the incident. [/Accent End]
    [ActionQuestionSound]
	*recap
		Try say use water.
	*reprompt
		Try say use water.
	*then
		hear use water, drink water {
			-> Finding Out
		}

@Finding Out
	*say
		The father has given you his word that the water is clean for drinking. But even without his promise you'd still chug the whole glass of water, bottoms-up.
        [Pause]
        The desert in your body is no more. Right now it is a lagoon of spring water, cool splashes of comfort travels to every corner of your body, like ice on melting lava. It's almost as if you could hear the hiss.
        [Man]
        Feel better? You really looked like the desert's going to get you.[/Accent End] 
        Now it's just the stench that's really
        giving you a headache.
        [Pause]
        [Man]
        Sorry for the bad manners back there. It's just this town has never since a single visitor for a long time. Not after the incident.[/Accent End]
        [ConversationQuestionSound]You should ask him.
	*reprompt
		You can talk to him by saying talk to the shopkeeper
	*recap
		You can talk to him by saying talk to the shopkeeper
	*then
		hear talk to the shopkeeper, what incident, incident, ask about the incident, ask, talk, talk to the shopkeeper, ask papa {
			-> Ask Shopkeeper
		}

@Ask Shopkeeper
	*say
		[Man]A while ago the town was famed for our Shawarma. People would come all the way from cities to this dune for a taste of it.[Pause] Say, what's your name?[/Accent End]
		[ConversationQuestionSound]
	*then
	    slot playerName_ to 'AMAZON.US_FIRST_NAME'
		hear {playerName_}, my name is {playerName_} {
			-> Ask for Help
		}

@Ask for Help
	*say
		[Man] 
		That's a rare name around here. Anyway, {playerName_},  this is a desert so you could imagine it never rains here- until that night. [Pause] On that night. We've seen the greatest thunderstorm of the town's history, but no-one anticipated what came after.[/Accent End]
        [Kid]
        And then the next morning, my dad started sweating like a cow. So did everyone else. But not me.[/Kid]
        [Man]
        Yeah. We were like broken faucets. But what's worse was what happens afterwards- that's right. the stench. This stench. It kept everyone from talking to each other. It drove tourists away. And now, no-one wants to come to our town anymore.[/Accent End]
        [Pause]
        The shopkeeper points to a poster on the left wall. It has some dust and the colors have started to fade away, but it's still pretty clear. 
        [Man]
        The mayor believed that it's a curse. He was trying to find people to help. Even set up a great reward for anyone who lifts the curse. 
        [Pause]
        Say, since you're here, why don't you help us? There's a lucrative treasure, and it could be all yours. In the meantime, you can look around the store. No worries.[/Accent End]
        It seems like they really need your help. Do you want to help them?
	*reprompt
		It seems like they really need your help.
	*recap
		It seems like they really need your help. Do you want to help them?
	*then
				hear yes, sure {
			-> Decide to Help
		}

@Mayor Office
	*say
		You've arrived at the mayor's office. It's the largest building in the town, but it's doors and windows are tightly shut and locked. When you get close to the front door, there's a sharp smell of hot pepper. Looks like there's no way in. If you don't know where to go, remember to say "What's around me".
    *recap
        If you don't know where to go, remember to say "What's around me".
    [DirectionQuestionSound]
	*then
		hear store, general store, go to the general store {
			-> Store
		}
		hear what is around me {
			-> Around_1
		}
	*recap
		Pick the other place
	*reprompt
		Pick a place you want to go to

@Look Left
	*say
		You wiped your face with your dry hands and decided to turn left. Then, you keep treading the sea of powdered rocks wearily.
	*then
		-> Tutorial 1A: Check Around

@Decide to Help
	*say
		[Man]
	    Ha-haaah! This is our lucky day, son. Finally a person who is willing to help us. Well, I guess I shouldn't be keeping here for too long, eh? [/Accent End]
		[Kid] 
		But papa, you haven't told him anything about how to solve the problem. [/Accent End]
		[Man] 
		Huh? Oh wait...Ah, yes, yes, yes. Must've been the heat and the stench, now I'm just rusty. Rusty! [/Accent End]
		The shopkeeper scratches his bald head. 
		[Man] 
		Yes... about where the problem is... hmmmm... let me think... [/Accent End]
		[Kid] 
		Papa, do you really know what the problem is? [/Accent End]
		[Man] 
		Ahh. Well, I've only heard stories and tales, but I guess I should let you know. [/Accent End]
		[Man] 
		So it all happened on one night... [/Accent End]
		[Kid] 
		You've already said that. [/Accent End]
		[Man]
		Oh have I? Oh yes, yes. Anyways, legend has it that a mysterious stone is the key ingredient of lifting this curse. It also says that it's buried deep in the a secret cavern in the valleys to the East. [Pause] Anyways, this is all I have. I know it's not much... hey, perhaps you can go ask if the mayor knows anything more. After all, he's the who put up this poster. Maybe he knows more.[/Accent End]
		[Kid] 
		But the mayor is also locking himself up. No-one has seen him for a while. [/Accent End]
		[Man] 
		Ha! This, my son, is when being the owner of the ONLY general store in the town comes in handy-- Kungh kungh, you see, the mayor has a secret craving for Super-Duper Hot Blue Pepper Sauce. And guess who's selling it? Me! Ha haaah! [/Accent End]
		Ask for one, or ask more about the sauce.
	*recap
		Ask for one, or ask more about the sauce.
	*then
		hear one, ask for one, ask one {
	        -> One Bottle of Sauce
	    }
	    hear more, ask for more, ask more {
	        -> More Sauce
	    }

@One Bottle of Sauce
	*say
		[Man]  
        One bottle of Super-Duper Hot Blue Pepper Sauce, coming right up! [/Accent End] 
        <audio src="https://koetest.s3.amazonaws.com/new_demo_stereo/findingThings1.mp3"/> 
        [Man]  
        Hmmm, where is it? [/Accent End] 
        <audio src="https://koetest.s3.amazonaws.com/new_demo_stereo/findingThings2.mp3"/> 
        [Kid]  
        Dad, didn't you just sell one to the Mrs. Chilirosa yesterday? [/Accent End] 
        [Man]  
        Hmm, Oh yes, yes! I did sold one to her. Oh dear. I guess that's our last bottle in a while. Next batch is not coming in a week, and I need to drive all the way out of town to get it because they can't stand our stench! [/Accent End] 
        [Kid]  
        Stop murmuring, dad. Anyways, if you go ask Mrs. Chilirosa, she might have still have some left for you.[/Accent End] 
        Now you can leave the store, or ask more about Mrs. Chilirosa.
	*recap
		Now you can leave the store, or ask more about Mrs. Chilirosa.
	*then
		hear leave the store {
            -> Leave the Store
        }
        hear ask more, ask more about Mrs Chilirosa {
            -> Ask more about Chilirosa
        }

@Ask more about Chilirosa
	*say
		[Man] 
        She's a stubborn old lady who works at the hair salon down the street. Like the mayor, she also has a craving for spicy things. [/Accent End]
        [Kid] 
        Yeah! All my friends who visited her shop came out with hair that smelled spicy for days! [/Accent End]
        [Man] 
        But I know what she also likes. As funny as it may sound, she also likes roses. But I doubt you'll be finding any of them around. Not in this desert.[/Accent End]
        Now you can leave the store, or ask again
	*recap
		Now you can leave the store, or ask again
	*then
		hear leave the store, leave {
            -> Leave the Store
        }
        hear ask again, ask {
            -> Ask more about Chilirosa
        }

@Leave the Store
	*say
		Just as you are about to leave the store, the shopkeeper stops you.
        [Man]
        Wait wait. The desert is hot and wide. Don't want you to starve or get thirsty out there. Take these.[/Accent End]
        The shopkeeper gives you a big bread and a full canteen of water. They both look edible, but at the same time the stench attaches to them. 
        //<audio src='https://koetest.s3.amazonaws.com/new_demo_stereo/doorOpen1.mp3'/>
        You step out of the store.
	*then
	    set currentLocation as 'a cross'
		-> The Cross

@The Cross
	*say
		[DirectionQuestionSound]There are a few things that you may want to check: A signpost, the mayor's office to your left, or you can go find the beauty salon.
	*then
		hear signpost, check the signpost {
		    set currentLocation as 'signpost'
            -> Signpost
        }
        hear mayor's office, check mayor's office {
            set currentLocation as 'mayors office'
            -> Mayor's Office
        }
        hear beauty salon, go find the beauty salon {
            set currentLocation as 'outside of the beauty saloon'
            -> Beauty Salon
        }

@Signpost
	*say
		You come forward to the signpost across the lonely street. There are a few items on it. It reads: the right leads to the mayor's office, Chilirosa's Beauty Salon, and the Eastern exit of the town. You've also noticed that there are a few strings of ivy tangling around the signpost. They look surprisingly healthy.
	*then
		-> The Cross

@Mayor's Office
	*say
		You arrived at the mayor's office. It's the seemingly largest building in the town, and it's doors are shut tight and locked. Beside the door is a trash can full of empty bottles. 
        [Pause]
        [Action]   Now you can: Knock, bash, or try to open the door, check out the trash can, or go to another location.
	*then
		hear knock {
            -> Knock On Mayor's Office
        }
        hear bash {
            -> Bash On Mayor's Office
        }
        hear check out the trash can, trash can {
            -> Check Out the Trash Can
        }
        hear go to another location, another location {
            set currentLocation as 'a cross'
            -> The Cross
        }

@Knock On Mayor's Office
	*say
		You knocked on the door, but nobody answers.
	*then
		-> Mayor's Office

@Bash On Mayor's Office
	*say
		You bashed heavily on the door, but nothing happened.
	*then
		-> Mayor's Office

@Check Out the Trash Can
	*say
		You checked out the trash can, but it's empty.
	*then
		-> Mayor's Office

@Beauty Salon
	*say
		You started walking towards the end of the street.
        //<audio src='https://koetest.s3.amazonaws.com/new_demo_stereo/windBlowing1.mp3'/>
        Nothing but the sandy gusts of wind blow into your ears. No-one is visible on the street. The buildings on the way look pretty modern, but they are all coated with a thick layer of dust. While you're on your way, you noticed something peculiar in the corner of one of the buildings. It's a patch of green. 
        //<audio src='https://koetest.s3.amazonaws.com/new_demo_stereo/footsteps2.mp3'/>
	*then
	    unflag flower
		-> Arrive Beauty Salon

@Arrive Beauty Salon
	*say
		You've now arrived at Chilirosa's Beauty Salon. It's clearly outdated design makes it stand out from other establishments on the street.
        [Action]Now you can: Knock on the door, bash on the door, open the door directly, or check on the green patch before.
	*then
		hear knock, knock on the door {
		    set currentLocation as 'inside of the beaty saloon'
            -> Knock On Beauty Salon
        }
        hear bash, bash on the door {
            set currentLocation as 'inside of the beaty saloon'
            -> Bash On Beauty Salon
        }
        hear open the door directly {
            -> Open the Beauty Salon Directly
        }
        hear check the green patch before, green patch, check {
            -> check the Green Patch
        }

@Knock On Beauty Salon
	*say
		You gently knocked on the door.
        //<audio src='https://koetest.s3.amazonaws.com/new_demo_stereo/flowfootsteps.mp3'/>
        [MrsChilirosa]
        Alright, alright, coming.[/Accent End]
        //<audio src='https://koetest.s3.amazonaws.com/new_demo_stereo/doorOpen2.mp3'/>
	*then
		if flower {
            -> Meet Mrs Chilirosa with flower
        }
        -> Meet Mrs Chilirosa

@Bash On Beauty Salon
	*say
		You bashed on the door.
        //<audio src='https://koetest.s3.amazonaws.com/new_demo_stereo/peopleWalkingFast.mp3'/>
        [MrsChilirosa]
        Show some respect! Don't you have any manners?[/Accent End]
        //<audio src='https://koetest.s3.amazonaws.com/new_demo_stereo/doorOpen2.mp3'/>
	*then
		if flower {
            -> Meet Mrs Chilirosa with flower
        }
        -> Meet Mrs Chilirosa

@Meet Mrs Chilirosa with flower
	*say
		[MrsChilirosa]
        Oh, my! Is THAT for me?[/Accent End]
	*recap
		[MrsChilirosa]
        I believe it is for me, right?[/Accent End]
	*then
		hear yes, sure {
            -> Give Mrs Chilirosa the flower
        }

@Give Mrs Chilirosa the flower
	*say
		[MrsChilirosa]
        Why, thank you, dear... What's your name? [/Accent End]
        <amazon:effect name="whispered">say your name!</amazon:effect>.
	*recap
		<amazon:effect name="whispered">say your name!</amazon:effect>.
	*reprompt
		<amazon:effect name="whispered">say your name!</amazon:effect>.
	*then
		slot playerName to 'AMAZON.US_FIRST_NAME'
		hear {playerName}, my name is {playerName} {
			-> Answer Your Name
        }

@Answer Your Name
	*say
		[MrsChilirosa]
        Oh, thank you, dear {playerName}. Come in, come in. We've haven't had guests in quite a while. [/Accent End]
        Mrs. Chilirosa smells the rose with all her breath. 
        [MrsChilirosa]
        Ho, ho, ho, what a wonderful smell. Now, tell me, dear, what kind of hairstyle are we looking for today? [/Accent End]
	*then
		-> Meet Mrs Chilirosa

@Meet Mrs Chilirosa
	*say
		[ConversationQuestionSound]Now, it's time to tell her about the sauce. You can try to say "Ask about the sauce".
	*recap
		You can try to say "Ask about the sauce".
	*reprompt
		She is waiting for your response, just say:"Ask about the sauce"
	*then
		hear ask about the sauce {
            -> Ask about the Sauce
        }

@Ask about the Sauce
	*say
		[MrsChilirosa]
        Ah yes, the Super-Duper Hot Blue Pepper Sauce. I can't eat with out it. It's my fountain of life. I need it for my breakfast, lunch and supper! People don't understand the beauty of the sauce, but that's ok! That means I'll only have to share it with the few that understands. Hohoho! But, you see, it's kind of hard to get it around this area, so I just wanted to ask why you needed the sauce. [/Accent End]
        [ConversationQuestionSound]So we have two plans here: Plan A, You tell her how you like the sauce; Plan B, tell her that you need it to get the mayor's attention. Which plan you wanna go?
	*recap
		We have two plans here: Plan A, You tell her how you like the sauce; Plan B, tell her that you need it to get the mayor's attention. Which plan you wanna go?
	*then
		hear A, plan A, tell her how I like the sauce, like the sauce {
            -> Tell Mrs Chilirose you like the Sauce
        }
		hear B, plan B, tell her that I need it to get the mayor's attention, need {
			-> Tell Mrs Chilirose Honestly
        }

@Tell Mrs Chilirose you like the Sauce
	*say
		[MrsChilirosa]
        Oh! Really? but why? [/Accent End]
        Ok. Now make up a good reason why you like the sauce. You can say the sentence start with [Pause]"My reason is"
	*recap
		Now make up a good reason why you like the sauce. You can say the sentence start with [Pause]"My reason is"
	*reprompt
		Ok. Now make up a good reason why you like the sauce. You can say the sentence start with [Pause]"My reason is"
	*then
		hear my reason is {
            -> Tell Mrs Chilirose you like the Sauce 2
        }

@Tell Mrs Chilirose you like the Sauce 2
	*say
		[MrsChilirosa]
        I can't believe it! It's another friend who appreciates Super-Duper Sauce like I do! Ho, ho, ho! Today has been the best day in years! Here. You can have the rest. [/Accent End]
        She gleefully hands over the hot sauce to you. You exit the building.
	*then
		-> Get the Hot Sauce

@Tell Mrs Chilirose Honestly
	*say
		[MrsChilirosa]
        Hmmm. I see. Truly, the town isn't seeing anyone over these years, just because of this smell. Ew! It reminds me of exactly someone who had this smell a few years back. Anyways, if I can help you get rid of this smell, I'll help however I can. Here, take it.[/Accent End]
        She hands over the hot sauce to you. You exit the building.
	*then
	    set currentLocation as 'mayors office'
		-> Get the Hot Sauce

@Open the Beauty Salon Directly
	*say
		you tried to open the door directly, but it's not opening
	*then
		-> Arrive Beauty Salon

@check the Green Patch
	*say
		You walk back to the patch of green sitting in the corner of a building. Oh dear, in a dry desert like this, you found a rose with a bright red color on it. You pick up the rose and hold it in your hand.
	*then
		flag flower
        -> Arrive Beauty Salon

@Get the Hot Sauce
	*say
		Now you have the hot sauce. You can try getting the mayor's attention. But how?
        [Action]Wave to the window on the streets? Or shout at the mayor?
	*reprompt
		What should we do? Wave to the window on the streets? Or shout at the mayor?
	*recap
		Wave to the window on the streets? Or shout at the mayor?
	*then
		hear wave to the window, wave {
            -> Wave To the Window
        }
        hear shout, shout at the mayor {
            -> Shout At the Mayor
        }

@Wave To the Window
	*say
		You waved to the window, but nothing happened.
	*then
		-> Get the Hot Sauce

@Shout At the Mayor
	*say
		Ok then. Shout something loud and clear so that the mayor can hear you. Now let's shout![ConversationQuestionSound]
	*recap
		Seems like he is not interested in what you said. Shout something else? maybe mention that you have the super duper hot pepper sauce? [ConversationQuestionSound]
	*reprompt
		Ok then. Shout something loud and clear so that the mayor can hear you. Now let's shout![ConversationQuestionSound]
	*then
		hear super duper hot blue pepper sauce, sauce, hot , pepper, super duper, hot sauce, pepper sauce {
			-> Shout At the Mayor_1
		}

@Shout At the Mayor_1
	*say
		[Mayor]
        Who's shouting?! Did I just hear Super-Duper Hot Blue Pepper Sauce![/Accent End]
        The mayor finds you below his window. 
        [Mayor] 
        Say, you said you have super-duper hot blue sauce? Come in! Door's unlocked. Remember to shut the door on your way in! [/Accent End]
        You enter the mayor's office, and shut the door swiftly as come indoors. There is a different smell in this house. You don't smell the stench anymore; rather, it is replaced with the strong, spicy scent of the hot sauce. As you make your way to the mayor's room, you find that hot sauce is being spilled all over the place. Hallways, corners and especially under doors. 

        [Mayor]
        Ah. Welcome. I see that you've brought some hot sauce for me?[/Accent End]
        You hand over the sauce. The mayor quickly jerks open the bottle cap, and walks hastily to one of the doors and pours the sauce under it. 
        [Mayor]
        You see. I actually don't like the sauce. I use it only to block off smell. This fog of evilness has been surrounding this town for so long that I almost forgot when it happened. Say, why did you come?[/Accent End]
        Let me see... you come here because you need more information on the smell. right?
	*then
		hear yes, right {
			-> Agree to Help the Mayor
		}
	*recap
		Wait, you come here because you need more information on the smell. right?
	*reprompt
		Wait, you come here because you need more information on the smell. right?

@Agree to Help the Mayor
	*say
		[Mayor]Wait. Are you saying that you're willing to help us get rid of this disgusting smell? Oh please, sit down, sit down[/Accent End]

        You sit down on the old couch in the mayor's office. The couch releases a spicy smell that stings.  
      
    [Mayor]
        So, the legend out there says there's a precious gem hidden in the valleys to the East, and that the precious gem is the key ingredient to lifting the curse.[/Accent End]
        //<audio src="https://koetest.s3.amazonaws.com/new_demo_stereo/windBlowing2.mp3"/> 

    [Mayor]    that the don't know, is that the cavern is guarded by puzzles and challenges. But what's beyond that- nobody knows. Nobody remembers[/Accent End]
	*then
	    unflag cave
	    unflag town
		-> Ask the Mayor

@Ask the Mayor
	*say
		[ConversationQuestionSound]You can ask about how he knows all this, about the cave, or about the town itself.
	*recap
		Well, the options are: A. ask about how he knows all this; B. ask about the cave; C. ask about the town it self.
	*then
		if cave and town {
            -> Before Leave For the Cave
        }
		hear A, a, how he knows all this, all this {
			-> Ask Mayor How He knows
        }
        hear B, b, about the cave, cave {
            -> Ask Mayor The Cave
        }
        hear C, c, about the town, town {
            -> Ask Mayor The Town
        }

@Ask Mayor How He knows
	*say
		[Mayor]
        A while ago we sent our best people to go and retrieve the stone. When they returned, they were covered in smell even nastier than what we have in the town. But what's worse was that they could barely remember anything. The doctor said that probably the smell was too unbearable that they passed out at some point and lost all the memory. This is all they could remember.[/Accent End]
	*then
		-> Ask the Mayor

@Ask Mayor The Cave
	*say
		[Mayor]
        Well, there's not much to talk about the cave, other than it's hidden in the valleys to the East... wait. Ah, yes, nearly missed this: It is said that there were plant roots that lead into the cave. A real miracle since green is sparse in the desert.[/Accent End]
	*then
		flag cave
        -> Ask the Mayor

@Ask Mayor The Town
	*say
		[Mayor]
        We were once a happy little town in the desert. At one point it even became a tourist spot! It's crazy to think that people would come from everywhere, just to get a taste of our famous dish, Shawarma.[/Accent End]
      [ConversationQuestionSound]  Do you want to continue on this subject? Or ask him about something else?
	*recap
		You can answer yes or no. Do you want to continue on this subject?
	*then
		hear yes, sure, of course {
            -> Continue Ask Mayor The Town
        }
        hear no, ask, ask him about something else {
            -> Ask the Mayor
        }

@Continue Ask Mayor The Town
	*say
		[mayor]
        Speaking of which, this reminds me of a character, yes, quite a character in our town. [/Accent End]
        The mayor pauses to sip a cup of water. He also spreads some of the hot sauce on his nose. 
        [Mayor] 
        We had this guy who worked in one of the most famous Shawarma places in town. Everything was normal except he sweats a lot, and a lot. It's like he wasn't born for the desert. [/Accent End]
        The hot sauce made the mayor to sweat a little bit, as he tries to wipe of some of them from his cheeks.
        [Mayor]
        But it doesn't just stop there. Oh dear, he has this odor. It was offending the guests. A great embarrassment. [/Accent End]
        The mayor leans back and waves his hands while he talks. 
        [Mayor] So, in the end, we had to make him leave. That's just a story. I don't know if anyone has hired him again, but that was just some time before this smell found its way into the town. [/Accent End]
	*then
		flag town
        -> Ask the Mayor

@Before Leave For the Cave
	*say
		and you can also heave for the cave.[DirectionQuestionSound]
	*recap
		Well, the options are: A. leave for the cave; B. ask mayor about something else
	*then
		hear A, a, leave, leave for the cave {
            -> Leave the Cave
        }
        hear B, b, ask, ask mayor about something else, something else {
            -> Ask Mayor The Cave
        }
@Leave the Cave
	*say
		[Mayor]
            I hope that these information are useful to you. We're all looking forward to seeing the town coming back to life some day. Good luck! [/Accent End]
        You leave the mayor's office, back on to the streets. As you head East for you journeys, you look back again at the small town, still suffering from the bad smell. You also see the boy from the general store waving at you as you stretch into the horizon in the East. This is the end of this demo. Thanks for playing!
	*then
		>> END

@More Sauce
	*say
		The shopkeeper seems not happy about that, maybe we should ask for one bottle for this time?
	*then
		hear yes, sure {
			-> One Bottle of Sauce
		}
		hear no {
			-> More Sauce
		}

@global append
	*then
		hear restart, start over {
            >> RESTART
        }
        hear jump to saloon {
            -> Leave the Store
        }
        hear get the hot sauce, I have your sauce, I have the hot sauce {
            set currentLocation as 'Mayors Office'
            -> Get the Hot Sauce
        }
        hear where am I, current location, tell me my location {
            -> Where am I
        }
        hear help {
            -> Help
        }
        
@Where am I
    *say
        Now you are in {currentLocation}.
    *then
        >> BACK
        
@Help
    *say
        Here is what you can do: 
            if you wanna know your current location, say: where am I; 
            if you wanna restart the game, say: restart; 
            if you wanna skip to the saloon scene, say: jump to saloon;
            if you wanna skip to the mayor's office scene, say: get the hot sauce.
    *then 
        >> BACK
